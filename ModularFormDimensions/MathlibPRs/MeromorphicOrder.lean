import Mathlib.Analysis.Meromorphic.Order
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.Calculus.Deriv.Pow

/-!
# Mathlib PR #33000: analytic order of a composition
-/
open Filter Topology

@[simp]
protected lemma ENat.map_mul {R : Type*} [NonAssocSemiring R] [DecidableEq R] [CharZero R]
    (a b : â„•âˆ) :
    (map Nat.cast (a * b) : WithTop R) = map Nat.cast a * map Nat.cast b :=
  map_mul ((Nat.castRingHom R : â„• â†’*â‚€ R).ENatMap Nat.cast_injective) a b

variable {ğ•œ E : Type*} [NontriviallyNormedField ğ•œ] {f : ğ•œ â†’ E}
  [NormedAddCommGroup E] [NormedSpace ğ•œ E]

theorem AnalyticAt.analyticOrderAt_sub_eq_one_of_deriv_ne_zero {x : ğ•œ} (hf : AnalyticAt ğ•œ f x)
    (hf' : deriv f x â‰  0) : analyticOrderAt (f Â· - f x) x = 1 := by
  generalize h : analyticOrderAt (f Â· - f x) x = r
  cases r with
  | top =>
    simp_rw [analyticOrderAt_eq_top, sub_eq_zero] at h
    refine (hf' ?_).elim
    rw [EventuallyEq.deriv_eq h, deriv_const]
  | coe r =>
    norm_cast
    obtain âŸ¨F, hFa, hFne, hfFâŸ© := (analyticOrderAt_eq_natCast (by fun_prop)).mp h
    apply eq_of_ge_of_le
    Â· by_contra! hr
      have := hfF.self_of_nhds
      simp_all
    Â· contrapose! hf'
      simp_rw [sub_eq_iff_eq_add] at hfF
      rw [EventuallyEq.deriv_eq hfF, deriv_add_const, deriv_fun_smul (by fun_prop) (by fun_prop),
        deriv_fun_pow (by fun_prop), sub_self, zero_pow (by omega), zero_pow (by omega),
        mul_zero, zero_mul, zero_smul, zero_smul, add_zero]

section comp
/-!
## Vanishing order of a composition of functions
-/
variable {x : ğ•œ} {f : ğ•œ â†’ E} {g : ğ•œ â†’ ğ•œ}

lemma eventuallyConst_iff_analyticOrderAt_sub_eq_top :
    EventuallyConst f (ğ“ x) â†” analyticOrderAt (f Â· - f x) x = âŠ¤ := by
  simpa [eventuallyConst_iff_exists_eventuallyEq, analyticOrderAt_eq_top, sub_eq_zero]
    using âŸ¨fun âŸ¨c, hcâŸ© â†¦ (show f x = c from hc.self_of_nhds) â–¸ hc, fun h â†¦ âŸ¨_, hâŸ©âŸ©

/-- If `g` is analytic at `x`, `f` is meromorphic at `g x`, and `g` is not locally constant near
`x`, the order of `f âˆ˜ g` is the product of the orders of `f` and `g`. -/
lemma MeromorphicAt.meromorphicOrderAt_comp (hf : MeromorphicAt f (g x)) (hg : AnalyticAt ğ•œ g x)
    (hg_nc : Â¬EventuallyConst g (ğ“ x)) :
    meromorphicOrderAt (f âˆ˜ g) x =
      (meromorphicOrderAt f (g x)) * (analyticOrderAt (g Â· - g x) x).map Nat.cast := by
  -- First deal with the silly case that `f` is identically zero around `g x`.
  rcases eq_or_ne (meromorphicOrderAt f (g x)) âŠ¤ with hf' | hf'
  Â· rw [hf', WithTop.top_mul]
    Â· rw [meromorphicOrderAt_eq_top_iff] at hf' âŠ¢
      rw [Function.comp_def, â† eventually_map (P := fun x â†¦ f x = 0)]
      exact EventuallyEq.filter_mono hf' (hg.map_nhdsNE hg_nc)
    Â· simp [(show AnalyticAt ğ•œ (g Â· - g x) x by fun_prop).analyticOrderAt_eq_zero]
  -- Now the interesting case. First unpack the data
  have hr := (WithTop.coe_untopâ‚€_of_ne_top hf').symm
  rw [meromorphicOrderAt_ne_top_iff hf] at hf'
  set r := (meromorphicOrderAt f (g x)).untopâ‚€
  rw [hr]
  -- Now write `F = (Â· - g x) ^ r â€¢ G` for `G` analytic and nonzero at `g x`
  obtain âŸ¨F, hFan, hFne, hFevâŸ© := hf'
  have aux1 : f âˆ˜ g =á¶ [ğ“[â‰ ] x] (g Â· - g x) ^ r â€¢ (F âˆ˜ g) :=
    hFev.comp_tendsto (hg.map_nhdsNE hg_nc)
  have aux2 : meromorphicOrderAt (F âˆ˜ g) x = 0 := by
    rw [AnalyticAt.meromorphicOrderAt_eq (by fun_prop),
      analyticOrderAt_eq_zero.mpr (by exact .inr hFne), ENat.map_zero, CharP.cast_eq_zero,
      WithTop.coe_zero]
  rw [meromorphicOrderAt_congr aux1,
    meromorphicOrderAt_smul ?_ (AnalyticAt.meromorphicAt <| ?_), aux2, add_zero,
    meromorphicOrderAt_zpow, AnalyticAt.meromorphicOrderAt_eq] <;>
  fun_prop

/-- Analytic order of a composition of analytic functions. -/
lemma AnalyticAt.analyticOrderAt_comp (hf : AnalyticAt ğ•œ f (g x)) (hg : AnalyticAt ğ•œ g x) :
    analyticOrderAt (f âˆ˜ g) x = analyticOrderAt f (g x) * analyticOrderAt (g Â· - g x) x := by
  -- For most cases we can use the `meromorphicOrderAt` lemma, but this version is also true
  -- if `g` is locally constant (unlike the meromorphic version) so we must prove this case.
  by_cases hg_nc : EventuallyConst g (ğ“ x)
  Â· have := hg_nc.comp f
    rw [eventuallyConst_iff_analyticOrderAt_sub_eq_top] at hg_nc this
    rw [hg_nc]
    by_cases hf' : f (g x) = 0
    Â· simpa [hf', show analyticOrderAt f (g x) â‰  0 by grind [analyticOrderAt_ne_zero]]
    Â· rw [show analyticOrderAt f (g x) = 0 from ?_, zero_mul] <;>
      grind [hf.comp hg, AnalyticAt.analyticOrderAt_eq_zero]
  simpa [hf.meromorphicOrderAt_eq, (hf.comp hg).meromorphicOrderAt_eq, â† ENat.map_mul]
    using hf.meromorphicAt.meromorphicOrderAt_comp hg hg_nc

/-- If `g` is analytic at `x`, `f` is meromorphic at `g x`, and `g' x â‰  0`, then the order of
`f âˆ˜ g` at `x` is the order of `f` at `g x`. -/
lemma MeromorphicAt.meromorphicOrderAt_comp_of_deriv_ne_zero
    (hf : MeromorphicAt f (g x)) (hg : AnalyticAt ğ•œ g x) (hg' : deriv g x â‰  0) :
    meromorphicOrderAt (f âˆ˜ g) x = meromorphicOrderAt f (g x) := by
  have hgo : analyticOrderAt _ x = 1 := hg.analyticOrderAt_sub_eq_one_of_deriv_ne_zero hg'
  rw [hf.meromorphicOrderAt_comp hg, hgo] <;>
  simp [eventuallyConst_iff_analyticOrderAt_sub_eq_top, hgo]

/-- If `g` is analytic at `x`, `f` is meromorphic at `g x`, and `g' x â‰  0`, then the order of
`f âˆ˜ g` at `x` is the order of `f` at `g x`. -/
lemma AnalyticAt.analyticOrderAt_comp_of_deriv_ne_zero {g : ğ•œ â†’ ğ•œ}
    (hf : AnalyticAt ğ•œ f (g x)) (hg : AnalyticAt ğ•œ g x) (hg' : deriv g x â‰  0) :
    analyticOrderAt (f âˆ˜ g) x = analyticOrderAt f (g x) := by
  simp [hf.analyticOrderAt_comp hg, hg.analyticOrderAt_sub_eq_one_of_deriv_ne_zero hg']

end comp
